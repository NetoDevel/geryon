buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        maven { url 'http://jcenter.bintray.com' }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'net.researchgate:gradle-release:2.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: "maven-publish"
apply plugin: 'net.researchgate.release'

def projectName = 'geryon'

task fatJar(type: Jar) {
    archiveName = projectName + '-all.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

bintray {
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayKey')

    pkg {
        repo = 'releases'
        name = 'geryon'
        userOrg = 'geryon'
        licenses = ['MIT']

        version {
            name = project.version
            desc = 'Geryon ' + project.version
            released  = new Date()
            vcsTag = project.version
        }

        publications = ['java']
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifact fatJar
            groupId 'org.geryon'
            artifactId 'geryon'
            version project.version
        }
    }
}

repositories {
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    maven { url 'http://jcenter.bintray.com' }
    maven { url 'http://timboudreau.com/builds/plugin/repository/everything/'}
    mavenCentral()
}

afterReleaseBuild.dependsOn bintrayUpload

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'io.netty', name: 'netty-all', version: '4.1.12.Final'

    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile 'io.kotlintest:kotlintest:2.0.3'

    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    //unirest libs
    testCompile 'com.mashape.unirest:unirest-java:1.4.9'
    testCompile 'org.apache.httpcomponents:httpclient:4.3.6'
    testCompile 'org.apache.httpcomponents:httpmime:4.3.6'
    testCompile 'org.apache.httpcomponents:httpasyncclient:4.0.2'
    testCompile 'org.json:json:20140107'
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

